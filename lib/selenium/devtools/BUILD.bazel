load("@bazel_skylib//rules:run_binary.bzl", "run_binary")
load("@rules_ruby//ruby:defs.bzl", "rb_binary", "rb_library")

package(default_visibility = ["//rb:__subpackages__"])

CDP_VERSIONS = [
    "v85",
    "v119",
    "v120",
    "v121",
]

rb_library(
    name = "devtools",
    data = [":cdp-" + n for n in CDP_VERSIONS],
    deps = [":version"],
)

rb_library(
    name = "version",
    srcs = ["version.rb"],
)

rb_binary(
    name = "cdp-generate",
    srcs = ["support/cdp_client_generator.rb"],
    data = [
        "support/cdp/domain.rb.erb",
        "support/cdp/loader.rb.erb",
    ],
    main = "support/cdp_client_generator.rb",
)

[
    run_binary(
        name = "cdp-" + version,
        srcs = [
            "//common/devtools/chromium/" + version + ":browser_protocol",
            "//common/devtools/chromium/" + version + ":js_protocol",
        ],
        outs = [
            version,
            version + ".rb",
        ],
        args = [
            "$(location //common/devtools/chromium/" + version + ":browser_protocol)",
            "$(location //common/devtools/chromium/" + version + ":js_protocol)",
            "$(location :" + version + ")/",
            "$(location :" + version + ".rb)",
            version,
        ],
        tool = ":cdp-generate",
    )
    for version in CDP_VERSIONS
]
